buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

allprojects {
    repositories {
        jcenter()
        maven{
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

def setupServerProject(plugin, int versionCodeId, String versionNameValue, String packageName) {
    plugin.apply plugin: 'com.android.application'
    plugin.apply plugin: 'com.neenbedankt.android-apt'
    plugin.android {
        compileSdkVersion 23
        buildToolsVersion "23.0.1"
        defaultConfig {
            applicationId packageName
            minSdkVersion 16
            targetSdkVersion 23
            versionName versionNameValue
            versionCode versionCodeId
        }
        buildTypes {
            release {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        sourceSets.main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
        plugin.android {
            packagingOptions {
                exclude 'META-INF/LICENSE.txt'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
        }
    }
    plugin.dependencies {
        compile plugin.fileTree(include: ['*.jar'], dir: 'libs')
        apt 'com.google.dagger:dagger-compiler:2.0'
        compile 'javax.inject:javax.inject:1'
        compile 'javax.annotation:javax.annotation-api:1.2'
        compile 'com.google.dagger:dagger:2.0'
        compile plugin.project(':Library')
    }
    // Temporary while gradle does not fully support ndk
    plugin.android.applicationVariants.all { variant ->
        def ndkBuild = plugin.android.ndkDirectory.absolutePath + "/ndk-build"
        def jniDir = plugin.file("src/main/jni").absolutePath
        variant.javaCompile.dependsOn plugin.tasks.create(
                name: "build_" + (String)variant.name, type: Exec
        ) {
            commandLine ndkBuild, '-C', jniDir
        }
        plugin.clean.dependsOn plugin.tasks.create(name: "clean_" + (String)variant.name, type: Exec) {
            commandLine ndkBuild, '-C', jniDir, 'clean'
        }
    }
}