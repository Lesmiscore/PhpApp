apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.esminis.server.php"
        minSdkVersion 16
        targetSdkVersion 21
        versionName "1.7.1"
        versionCode 32 // should increment by 2 since 20
    }
    buildTypes {
        release {proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'}
    }
    productFlavors {
        arm { versionCode android.defaultConfig.versionCode - 1 }
        x86 {}
    }
    signingConfigs {}
    // Temporary while gradle does not fully support ndk
    sourceSets.arm {
        jniLibs.srcDir 'src/arm/libs'
        jni.srcDirs = []
    }
    sourceSets.x86 {
        jniLibs.srcDir 'src/x86/libs'
        jni.srcDirs = []
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.android.support:design:22.2.0'
    compile 'io.reactivex:rxandroid:0.25.0'
}

// Temporary while gradle does not fully support ndk
android.applicationVariants.all { variant ->
    def ndkBuild = android.plugin.ndkFolder.absolutePath + "/ndk-build"
    def jniDir = file("src/" + variant.properties.get('flavorName') + "/jni").absolutePath
    variant.javaCompile.dependsOn project.tasks.create(name: "build_" + variant.name, type: Exec) {
        commandLine ndkBuild, '-C', jniDir
    }
    clean.dependsOn project.tasks.create(name: "clean_" + variant.name, type: Exec) {
        commandLine ndkBuild, '-C', jniDir, 'clean'
    }
}